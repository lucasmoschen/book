Nós podemos querer saber o comportamento de uma função em algum subconjunto
$A$ de  $X$. Por exemplo, podemos dizer que $f$ é injetiva em $A$ se para todo
$x_1$ e $x_2$ em A, $f(x_1) = f(x_2) $ implicar $x_1 = x_2 $, por exemplo. A
diferença com relação à função parcial é que nesse caso, a função pode ser
definida em seu complementar. 

\begin{definition}
    \label{def8}
    Se $f$ é função de $X$ e $Y$, dizemos que $f[A]$ denota a imagem de $f$ em
    $A$, definido por $f[A] = \{y \in Y | ~\exists x \in A, y = f(x)\}$.
\end{definition}

\begin{theorem}
    \label{prop7}
    Seja $f: X \to Y $ e $A$ um subconjunto de $X$. Então, para todo $x$ em
    $A$, $f(x)$ está em $f[A]$.
\end{theorem}

\begin{proof}
    Por definição, $f(x) \in f[A] $ se, e somente se, existe $x'$ em $A$ tal
    que $f(x') = f(x)$. Isto vale para  $x' = x $.
\end{proof}

Em Lean, utilizando Táticas, esta prova pode ser apresentada da seguinte
forma: 

\begin{lstlisting}
import data.set 
open function

variables {X Y : Type}

example (f: X → Y) (A : set X) (a : X) (h: a ∈ A) : (f a) ∈ f '' A :=
begin 
    apply exists.intro a, 
    exact and.intro h (eq.refl (f a)),  
end   
\end{lstlisting}

\begin{theorem}
    \label{prop8}
    Seja $f: X \to Y$ e $g: Y \to Z$. Seja $A$ subconjunto de $X$. Então
    $$(g \circ f)[A] = g[f[A]]$$
\end{theorem}

\begin{proof}
    Seja $z \in (g \circ f)[A]$. Então, para algum $x \in A$, $z = (g \circ
    f)(x) = g(f(x))$. Pelo que acabamos de provar na Proposição \ref{prop8},
    $f(x) \in f[A]$. Novamente, pelo que acabamos de provar, $g(f(x)) \in
    g[f[A]] $.

    Alternativamente, seja $z \in g[f[A]]$. Então, existe $y$ em $f[A]$ tal
    que $f(y) = z$. Como $y \in f[A]$, existe $x \in A$, tal que $f(x) = y$.
    Então $(g \circ f)(x) = g(f(x)) = g(y) = z$, então $z \in (g \circ f)[A] $
\end{proof}

Uma prova de que a composição de funções sobrejetivas é sobrejetiva é a que
descrevemos cima, pois $f: X \to Y$ é sobrejetiva se, e somente se, $f[X] =
Y$.

Nós podemos ver $f$ como uma função de $A$, um subconjunto de $X$ a $Y$,
simplesmente ignorando o comportamento de $f$ nos elementos fora de $A$.

\begin{definition}
    \label{def9}
    Denotamos $f \upharpoonright A$ como restrição de $f$ para $A$. Isto é,
    dadas $f: X \to Y$ e $A \subseteq X$, $f \upharpoonright A : A \to Y$ é
    definida por $(f \upharpoonright A)(x) = x$, para todo $x$ em $A$.
\end{definition}

Agora, $f$ é injetiva em $A$ significa que a restrição de $f$ em $A$ é
injetiva.

\begin{definition}[Pré-imagem]
    \label{def10}
    Se $f: X \to Y$ e $B \subseteq Y$, então a pré-imagem de $B$ em $f$,
    denotado por $f^{-1}[B]$ é definida por $f^{-1}[B] = \{x \in X | f(x) \in
    B\}$. Ou seja, é o conjunto de elementos de $X$ que são mapeados em $B$.
\end{definition}

Note que essa definição faz sentido mesmo que $f$ não tenha inversa, visto que
dado $y \in B$ pode não haver $x \in X$ com a propriedade de $f(x) \in B$,
como podem ter vários. Se $f$ tem inversa ($f^{-1}$), então, para todo $y$ em
$B$, existe exatamente um elemento $x$ em $X$ com $f(x) \in B$. Neste caso,
dizemos que $f^{-1}[B]$ é a imagem de $B$ sobre $f^{-1}$ ou a pré-imagem de
$B$ sobre $f$.

Em Lean, a definição de Pré-imagem encontra-se na bilbioteca \textit{Mathlib}
e é definida da seguinte forma: 

\begin{lstlisting}
variables {X Y : Type}

def preimage (f : X → Y) (B : set Y) : set X := {x : X | f(x) ∈ B}

infix ` ⁻¹' `:80 := preimage
\end{lstlisting}


\begin{theorem}
    Seja $f: X \to Y$, $g: Y \to Z$ e $C \subseteq Z$. Então $$(g \circ
    f)^{-1}[C] = f^{-1}[g^{-1}[C]]$$.
\end{theorem}
\begin{proof}
    Para qualquer $y$, $y \in (g \circ f)^{-1}[C]$ se, e somente se, $g(f(y))$
    está em $C$. Isto acontece se, e somente se, $f(y) \in g^{-1}[C]$, o que
    acontece se, e somente se $y \in f^{-1}[g^{-1}[C]]$.
\end{proof}

Seguem algumas propriedades sobre imagens e pré-imagens. Aqui, $f$ denota uma
função arbitrária de $X$ em $Y$. $A, A_1, A_2, ...$ denotam subconjuntos
arbitrários de $X$, e $B, B_1, B_2,...$ denotam subconjuntos arbitrários de
$Y$.

\begin{enumerate}
    \label{iden}
    \item $A \subseteq f^{-1}[f[A]]$, e se $f$ é injetiva, $A = f^{-1}[f[A]]$.
    \item $f[f^{-1}[B]] \subseteq B$, e se $f$ é sobrejetiva, $B =
    f[f^{-1}[B]]$.
    \item Se $A_1 \subseteq A_2$, então $f[A_1] \subseteq f[A_2]$.
    \item Se $B_1 \subseteq B_2$, então $f^{-1}[B_1] \subseteq f^{-1}[B_2]$.
    \item $f[A_1 \cup A_2] = f[A_1] \cup f[A_2]$.
    \item $f^{-1}[B_1 \cup B_2] = f^{-1}[B_1] \cup f^{-1}[B_2]$.
    \item $f[A_1 \cap A_2] \subseteq f[A_1] \cap f[A_2]$ e, se $f$ é injetiva,
    $f[A_1 \cap A_2] = f[A_1] \cap f[A_2]$.
    \item $f^{-1}[B_1 \cap B_2] = f^{-1}[B_1] \cap f^{-1}[B_2]$.
    \item $f[A] \setminus f[B] \subseteq f[A\setminus B]$.
    \item $f[A] \cap B = f[A \cap f^{-1}[B]]$.
    \item $f[A \cup f^{-1}[B]] \subset f[A] \cup B $.
    \item $A \cap f^{-1}[B] \subseteq f^{-1}[f[A] \cap B]$.
    \item $A \cup f^{-1}[B] \subseteq f^{-1}[f[A] \cup B]$.
\end{enumerate}

A partir de agora, vamos demonstrar algumas dessas proporições, ora com
descrições da demonstração e ora com provas em Lean. Tambémm sugerimos que
essas proposições sejam exercícios para a leitora ou o leitor.

\subsection{Demonstrações com linguagem natural}

\begin{theorem}[Item 7]
    \label{exerc1}
    Sejam $X$ e $Y$ conjuntos, $A_1, A_2 \in X$ e $f: X \to Y$.
\end{theorem}

\begin{proof}
    Se $y \in f[A_1 \cap A_2]$, temos que existe $x \in A_1 \cap A_2$, com
    $f(x) = y$. Nesse caso, $f(x) \in f[A_1]$, e $f(x) \in f[A_2]$, o que
    demonstra a primeira parte.

    Agora, suponha a injetividade de $f$. Suponha também que $y \in f[A_1]
    \cap f[A_2]$. Assim, existem $x_1 \in A_1$ e $x_2 \in A_2$, com as
    propriedades de $f(x_1) = y = f(x_2)$. Como a função é injetiva, $f(x_1) =
    f(x_2) \Rightarrow x_1 = x_2$. Assim $x_1 \in A_2$, que implica $x_1 \in
    A_1 \cap A_2$. Logo, $y \in f[A_1 \cap A_2]$.
\end{proof}

\begin{theorem}[Item 10]
    \label{exerc2}
    Sejam $X$ e $Y$ conjuntos, $f: X \to Y, A \subseteq X$ e $B \subseteq Y$.
    Então, $f[A] \cap B = f[A \cap f^{-1}[B]]$
\end{theorem}

\begin{proof}
    Suponha, inicialmente, que $y \in f[A] \cap B$. Então $y \in B$ e para
    algum $x \in A$, $f(x) = y$. Nesse sentido, $x \in f^{-1}[B]$. Assim, $x
    \in A \cap f^{-1}[B]$ e, portanto, $y \in f[A \cap f^{-1}[B]]$.

    Alternativamente, se $y \in f[A \cap f^{-1}[B]]$, existe $x \in A \cap
    f^{-1}[B]$, com $f(x) = y$. Daqui, concluímos que $y = f(x) \in f[A]$ e $y
    \in B$, pela definição de pré-imagem. Então $y \in f[A] \cap B$, como
    queríamos provar.
\end{proof}

\begin{theorem}[Item 12]
\end{theorem}

\begin{proof}
    Suponha que $x \in A \and f^{-1}[B]$. Desta maneire, $x \in A$ e $x \in
    f^{-1}[B]$, que implica que existe $y \in B$, tal que $f(x) = y$. Nesse
    sentido, $y \in B$ e $y \in f[A]$, que implica $y \in f[A] \and B$. Como
    $f(x) = y $, então $x \in f^{-1}[f[A]\and B]$. 

\end{proof}

\subsection{Demontrações em Lean}

Nós podemos utilizar variáveis limitadas para falar sobre o comportamento de
funções em conjuntos particulares.

\begin{lstlisting}
import data.set -- inclui o símbolo de subconjunto da imagem ''
open set function

variables {X Y : Type}
variables (A  : set X) (B : set Y)

def maps_to (f : X → Y) (A : set X) (B : set Y) :=
    ∀ x ∈ A, f x ∈ B

def inj_on (f : X → Y) (A : set X) :=
    ∀ (x₁ ∈ A) (x₂ ∈ A), f x₁ = f x₂ → x₁ = x₂

def surj_on (f : X → Y) (A : set X) (B : set Y) :=
    B ⊆ f '' A

\end{lstlisting}

A definição de \lstinline{maps_on} é a ideia de que a imagem de um subconjunto
do domínio $X$ está totamente inclusa em um subconjunto específico do
contradomínio. As definições de \lstinline{inj_on} e \lstinline{surj_on} são
as definições usuais.

\begin{theorem}[Item 1]
\end{theorem}

\begin{lstlisting}
import data.set 
open function
open set 

variables {X Y : Type}

theorem item1.a (f : X → Y) (A : set X) : A ⊆  f ⁻¹' (f '' A) := 
assume a h, 
have h1: a ∈ A ∧ f a = f a, from and.intro h (eq.refl (f a)),
have h2: (f a) ∈ f '' A, from exists.intro a h1,   
show a ∈ f ⁻¹' (f '' A), from h2

theorem item1.b (f : X → Y) (A : set X) (h: injective f): 
        A =  f ⁻¹' (f '' A) :=
eq_of_subset_of_subset
(item1.a f A)
(assume x h1, 
have h2: f x ∈ f '' A, from h1,
have h3: ∃ (a : X), a ∈ A ∧ f a = f x, from h1,
show x ∈ A, from exists.elim h3
    (assume (y : X) (h4: y ∈ A ∧ f y = f x),
    have h5: y = x, from h h4.right,
    show x ∈ A, from eq.subst h5 h4.left)
)
\end{lstlisting}

\begin{theorem}[Item 3]
\end{theorem}

\begin{lstlisting}
import data.set
open set function

variables {X Y : Type}
variables (A B A₁ A₂ : set X)

theorem item3 (f: X → Y) : A₁ ⊆ A₂ → f '' A₁ ⊆ f '' A₂ :=
    assume h : A₁ ⊆ A₂,
    assume y,
    assume h₁ : y ∈ f '' A₁,
    have h₂ : ∃ x, x ∈ A₁ ∧ f(x) = y, from h₁,
    show y ∈ f '' A₂, from exists.elim h₂
        (assume (x' : X) (ha: x' ∈ A₁ ∧ f(x') = y ),
        have h₃ : x' ∈ A₂ ∧ f(x') = y, from and.intro (h ha.left)  ha.right,
        show y ∈ f '' A₂, from exists.intro x' h₃)    
\end{lstlisting}

\begin{theorem}[Item 9]
\end{theorem}

Observe que para o item 9, trataremos $X$ e $Y$ como conjuntos iguais. Caso
não sejam, $f[B]$ pode nem estar definida.

\begin{lstlisting}
import data.set
open set function

variables {X Y : Type}
variables (A B A₁ A₂ : set X)

theorem item9 (f: X → X) : f '' A \ f '' B ⊆ f '' (A \ B) :=
begin
    intros y h,
    have h₁ : y ∈ f '' A, from mem_of_mem_diff h,
    have h₂ : ¬ (y ∈ f '' B), from not_mem_of_mem_diff h,
    apply exists.elim h₁,
    intros x h₃,
    apply exists.intro x,
    apply and.intro,
    apply mem_diff_of_mem,
        exact h₃.left,
        assume h₄ : x ∈ B,
        exact false.elim (h₂ (exists.intro x (and.intro h₄ h₃.right))),
        exact h₃.right
end

#check @mem_of_mem_diff
#check @not_mem_of_mem_diff
#check @mem_diff_of_mem

\end{lstlisting}

As funções \lstinline{mem_of_mem_diff, not_mem_of_mem_diff} e
\lstinline{mem_diff_of_mem} tem o objetivo de lidar com a diferença e sua
relação com a interseção. Note que usar táticas auxilia o passo a passo, porém
dificulta o posterior entendimento. Por isso, é recomendável, nesses exemplos,
utilizar alguma ferramenta para Lean. Essas funções e outras já apresentadas,
encontram-se na biblioteca do Lean e grande parte delas está disponível quando
você abre o namespace \lstinline{function}.

\begin{lstlisting}
open function 

#check @comp 
#check @has_left_inverse
-- A right_inverse tem duas definicoes para Lean. Apenas uma
-- esta no namespace function. Por isso e importante especificar. 
#check @function.right_inverse    
\end{lstlisting}

\subsection{Definindo a inversa Classicamente}

Para definir funções inversas, é necessário que utilizemos o racionínio
clássico. 

\begin{lstlisting}
open classical 

#check @axiom_of_choice
#check @some_spec

variables A B : Type
variable P : A → Prop   
variable R : A → B → Prop

example : (∀ x, ∃ y, R x y) → ∃ f : A → B, ∀ x, R x (f x) :=
axiom_of_choice

example (h : ∃ x, P x) : P (some h) :=
some_spec h    
\end{lstlisting}

O axioma da escolha fala que se para todo \lstinline{x : X}, existe
\lstinline{y: Y} com \lstinline{R x y}, então existe uma função \lstinline{f: X → Y} que para todo \lstinline{x}, escolhe-se \lstinline{y}. Em Lean, é
utilizada a função \lstinline{some} para mostrar este axioma, através da
construção clássica. Podemos, portanto, definir a inversa como: 

\begin{lstlisting}
open classical function
local attribute [instance] prop_decidable

variables {X Y : Type}

noncomputable def inverse (f : X → Y) (default : X) : Y → X :=
λ y, if h : ∃ x, f x = y then some h else default   
\end{lstlisting}

Observe que a definição é não computacional, visto que como já argumentamos,
para uma determinada função, essa hipótese \lstinline{h} pode não ser possível
de validar algoritmicamente e, se a hipótese for válida, pode não ser possível
encontrar um valor de \lstinline{x} adequado, também algoritmicamente. Também
observe que essa inversa é definida assumindo \lstinline{y}, logo ela é
definida para todo valor que ele assume e retorna algum valor qye cumpre essa
propriedade \lstinline{f x = y}, quando a hipótese é válida, ou um valor
padrão dado inicialmente, caso não exista. O comando \lstinline{local attribute} 
fala para a instância que a regra \lstinline{prop_decidable} vai
ser utilizada no arquivo que se segue. Nesse caso, importamos os axiomas
clássicos e tornamos disponível a instância genérica da decição.   

Podemos, então, demonstrar os seguintes teoremas. 

\begin{lstlisting}
open classical function
local attribute [instance] prop_decidable

variables {X Y : Type}

noncomputable def inverse (f : X → Y) (default : X) : Y → X :=
λ y, if h : ∃ x, f x = y then some h else default

theorem inverse_of_exists (f : X → Y) (default : X) (y : Y)
    (h : ∃ x, f x = y) : f (inverse f default y) = y :=
    have h₁ : inverse f default y = some h, from dif_pos h,
    have h₂ : f (some h) = y, from some_spec h,
eq.subst (eq.symm h₁) h₂

#check @dite
#check @dif_pos 

-- Using Term Mode 
theorem is_left_inverse_of_injective (f : X → Y) (default : X)
    (injf : injective f) : left_inverse (inverse f default) f :=
let finv := (inverse f default) in
    assume x,
    have h1 : ∃ x', f x' = f x, from exists.intro x rfl,
    have h2 : f (finv (f x)) = f x, 
        from inverse_of_exists f default (f x) h1,
    show finv (f x) = x, from injf h2

-- Using Tatic Mode 
theorem is_left_inverse_of_injective2 (f : X → Y) (default : X)
    (injf : injective f) : left_inverse (inverse f default) f :=
begin 
    intro x,
    apply injf, 
    apply inverse_of_exists,
    apply exists.intro x,
    exact eq.refl (f x), 
end
\end{lstlisting}

Observado o significado de \lstinline{dite}, que expressa a validade de um
\lstinline{α : Sort}, independente de \lstinline{c: Prop} e o significado de
\lstinline{dif_pos}, que expressa a igualdade entre uma expressão do formato
\lstinline{dite} e outra do mesmo \lstinline{Sort}, podemos entender o
siginificado dessa prova. A versão em Táticas foi inserida como instrução de
aplicação em comparação como o modo em termos. 